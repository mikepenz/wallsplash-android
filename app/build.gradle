apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.mikepenz.unsplash"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 190
        versionName "1.9.0"
    }
    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled true

            proguardFile 'proguard-gson.pro'
            proguardFile 'proguard-square-picasso.pro'
            proguardFile 'proguard-square-retrofit.pro'
            proguardFile 'proguard-project.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = outputFile.name.replace(".apk", "-v" + defaultConfig.versionName + "-c" + defaultConfig.versionCode + ".apk")
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
}

dependencies {
    compile 'com.koushikdutta.ion:ion:2.1.7'

    compile 'com.android.support:palette-v7:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    //rxJava
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'

    compile 'com.nispok:snackbar:2.11.0'
    compile 'com.gu:option:1.3'

    compile 'com.github.lzyzsd:circleprogress:1.2.0@aar'
    compile 'com.github.xiprox.errorview:library:1.0.0'

    compile 'com.google.android.apps.muzei:muzei-api:+'

    compile('com.mikepenz:aboutlibraries:5.6.7@aar') {
        transitive = true
    }
    compile('com.mikepenz:materialdrawer:5.2.8@aar') {
        transitive = true
    }
    compile 'com.mikepenz:iconics-core:2.6.0@aar'
    compile 'com.mikepenz:google-material-typeface:2.2.0.1.original@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.6.0.1@aar'
}
